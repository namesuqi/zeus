*** Settings ***
Default Tags
Test Timeout      10 seconds
Variables         ../../../lib/constant/account.py
Variables         ../../../lib/constant/host.py
Variables         ../../../lib/constant/request.py
Variables         ../../../lib/constant/file.py
Library           ../../../lib/response/verify_response.py
Library           ../../../lib/response/parse_response.py
Library           ../../../lib/interface/up/dir_server.py
Library           ../../../lib/database/mysql_handler.py
Library           ../../../lib/database/mysql_db.py

*** Test Cases ***
delete_ok
    [Documentation]    目标：验证能够删除指定目录（包括该目录下的子目录及文件）
    ...
    ...    验证步骤：新建目录，在该目录下分别新建文件和子目录；删除该目录
    ...
    ...    预期结果：能够删除该目录及该目录下的文件和子目录
    [Tags]    dir    regression    api
    [Setup]    Run Keywords    Db Delete Folder    ${ID_LEIGANG}    /new    AND    Db Delete Folder
    ...    ${ID_LEIGANG}    /new/sub    AND    Db Delete File By Md5    ${NEW_FILE_MD5}
    Dir Create Folder    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${ID_LEIGANG}    /    new
    Dir Create Folder    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${ID_LEIGANG}    /new    sub
    Dir Create File By Path    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${ID_LEIGANG}    /new    ${NEW_FILE_NAME}
    ...    ${NEW_FILE_SIZE}    ${NEW_FILE_MD5}    ${NEW_FILE_ID}    yes
    ${response}    Dir Delete Directory    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${ID_LEIGANG}    /new
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    None
    ${exists}    Db Is Folder Exists    ${ID_LEIGANG}    /new
    Should Not Be True    ${exists}
    ${exists}    Db Is Folder Exists    ${ID_LEIGANG}    /new/sub
    Should Not Be True    ${exists}
    ${FileInfo}    Db Get Info By Md5    ${NEW_FILE_MD5}
    Should Be Empty    ${FileInfo}
    [Teardown]    Run Keywords    Db Delete Folder    ${ID_LEIGANG}    /new    AND    Db Delete Folder
    ...    ${ID_LEIGANG}    /new/sub    AND    Db Delete File By Md5    ${NEW_FILE_MD5}

delete_root_dir
    [Documentation]    目标：验证删除指定用户的根目录时，返回E_OP_FORBIDDEN
    [Tags]    dir    regression    api
    [Setup]
    ${response}    Dir Delete Directory    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${ID_LEIGANG}    /
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    E_OP_FORBIDDEN
    ${exists}    Db Is Folder Exists    ${ID_LEIGANG}    /
    Should Be True    ${exists}

invalid_directory
    [Documentation]    目标：验证要删除的目录不存在时，返回E_DIRECTORY_NON_EXISTS
    [Tags]    dir    regression    api
    [Setup]    Db Delete Folder    ${ID_LEIGANG}    /invalid
    ${response}    Dir Delete Directory    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${ID_LEIGANG}    /invalid
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    E_DIRECTORY_NON_EXISTS
