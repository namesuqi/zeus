*** Settings ***
Default Tags
Test Timeout      10 seconds
Variables         ../../../lib/constant/account.py
Variables         ../../../lib/constant/host.py
Variables         ../../../lib/constant/request.py
Variables         ../../../lib/constant/file.py
Library           ../../../lib/response/verify_response.py
Library           ../../../lib/response/parse_response.py
Library           ../../../lib/interface/up/dir_server.py
Library           ../../../lib/database/mysql_handler.py
Library           ../../../lib/database/mysql_db.py

*** Test Cases ***
search_ok
    [Documentation]    目标:验证能够根据有效url获取文件信息
    [Tags]    dir    regression    api
    ${response}    Dir Search By URL    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${CIWEN_PREFIX}    ${CIWEN_RELATIVE_URL}
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    None
    @{FileInfo}    Db Get Info By Url    ${CIWEN_PREFIX}    ${CIWEN_RELATIVE_URL}
    ${file_id}    Get Field Value    ${response}    file_id
    Should Be Equal    ${file_id}    @{FileInfo}[0]
    ${size}    Get Field Value    ${response}    size
    Should Be Equal    ${size}    @{FileInfo}[1]
    ${md5}    Get Field Value    ${response}    md5
    Should Be Equal    ${md5}    @{FileInfo}[2]
    ${source_url}    Get Field Value    ${response}    source_url
    Should Be Equal    ${source_url}    @{FileInfo}[3]

search_invalid_file
    [Documentation]    目标:验证URL前缀合法，文件名无效时，返回E_FILE_NON_EXISTS
    [Tags]    dir    regression    api
    ${response}    Dir Search By URL    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${CNTV_PREFIX}    ${INVALID_RELATIVE_URL}
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    E_FILE_NON_EXISTS

search_invalid_url_prefix
    [Documentation]    目标:验证URL前缀无效时返回E_INVALID_URL_PREFIX
    [Tags]    dir    regression    api
    ${response}    Dir Search By URL    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}    ${INVALID_PREFIX}    ${CNTV_RELATIVE_URL}
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    E_INVALID_URL_PREFIX

search_no_url
    [Documentation]    目标:验证不输入URL返回E_PARAM_MISSING
    [Tags]    dir    regression    api
    ${response}    Dir Search By URL    ${HTTP}    ${DIR_HOST}    ${DIR_PORT}
    Status Code Should Be Equal    ${response}    200
    Error Code Should Be Equal    ${response}    E_PARAM_MISSING
